buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "gt"
            url = "https://gregtech.overminddl1.com/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            name = "jitpack.io"
            url = "https://jitpack.io"
        }
        maven {
            name = 'ajoberstar-backup'
            url = 'https://ajoberstar.org/bintray-backup/'
        }
    }
    dependencies {
        classpath 'com.github.GTNH2:ForgeGradle:FG_1.2-SNAPSHOT'
    }
}

plugins {
  // project.grgit is not available and the repository we're working in is not 
  // interacted with: 
  id 'org.ajoberstar.grgit' version '4.0.2' apply false
}
// An explicit import is necessary because the plugin was not applied:
import org.ajoberstar.grgit.Grgit

apply plugin: 'forge'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'signing'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = "UTF-8"
}

def repo = Grgit.open()
project.ext.gitHash = repo.log().find().abbreviatedId
def branchname = repo.branch.getCurrent().fullName
if (branchname.contains('HEAD')) {
    branchname = System.getenv("GIT_BRANCH")
}
branchname = branchname.substring(branchname.lastIndexOf('/') + 1)

boolean dev = System.getenv("RELEASE") == null || System.getenv("RELEASE").equalsIgnoreCase("false")

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') == null ? "CUSTOM" : System.getenv('BUILD_NUMBER')

version = dev ? "${branchname}-${buildnumber}-${project.ext.gitHash}" : "${mod_version}.${buildnumber}"
group = "team.chisel" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Chisel"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"

    replaceIn "Chisel.java"
    replace "@VERSION@", project.version
}

configurations {
    shade
    compile.extendsFrom shade
    compile.extendsFrom exportedCompile
}

repositories {
    maven {
        name = "gt"
        url = "https://gregtech.overminddl1.com/"
    }

    maven {
        name 'Chicken Bones Repo'
        url "http://chickenbones.net/maven/"
    }

    maven {
        name 'Prof Mobius Repo'
        url "http://mobiusstrip.eu/maven"
    }
    
    maven {
        name 'Chisel Repo'
        url "http://maven.tterrag.com/"
    }
	    ivy {
        name = "gtnh_download_source_ee3"
        artifactPattern("http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]")
    }


}

dependencies {
    compile "mcp.mobius.waila:Waila:${waila_version}_${minecraft_version}:dev"
    compile "codechicken:CodeChickenLib:${minecraft_version}-${ccl_version}:dev"
    compile "codechicken:CodeChickenCore:${minecraft_version}-${ccc_version}:dev"
    compile "codechicken:NotEnoughItems:${minecraft_version}-${nei_version}:dev"
    compile "codechicken:ForgeMultipart:${minecraft_version}-${fmp_version}:dev"
    compile "com.pahimar.ee3:EquivalentExchange3:1.7.10-0.3.505:dev"
    
    shade ("team.chisel.ctmlib:CTMLib:${ctmlib_version}") {
        transitive = false
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**', '*.info'
        }
    }

    manifest {
        attributes 'FMLAT': 'chisel_compile_at.cfg'
    }
}

// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// Create API jar with sources and compiled files
task apiJar(type: Jar) {
    from sourceSets.main.allSource
    from sourceSets.main.output
    include 'com/cricketcraft/chisel/api/**/*'
    classifier = 'api'
}

// Create source attachment for dev jar
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives apiJar
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
            artifact apiJar
        }
    }

    repositories {
        if (project.hasProperty('mavendir')) {
            maven { url mavendir }
        }
    }
}

println('Changelog:\n' + getChangelogText())

String getChangelogText() {
    def changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.size() > 1) {
            def temp = it
            if (lineCount == 0) {
                temp = "Chisel ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

idea { module { inheritOutputDirs = true } }

